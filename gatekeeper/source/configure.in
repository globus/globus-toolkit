dnl

AC_REVISION($Revision$)

AC_INIT(Makefile.am)

GLOBUS_INIT

AM_PROG_LIBTOOL
AC_CONFIG_HEADERS(globus_gatekeeper_config.h)

dnl config header goes here

dnl Initialize the automake rules the last argument
AM_INIT_AUTOMAKE($GPT_NAME, $GPT_VERSION)

dnl Don't create a pkg-config file for program-only packages
pkgconffile=""

if test "x$GLOBUS_THREADS" != "xnone"; then
    AC_MSG_ERROR(Cannot build gatekeeper with threads)
fi

GLOBUS_OPENSSL

AC_CHECK_HEADERS(proj.h)
AC_CHECK_HEADERS(netinet/tcp.h)
AC_CHECK_HEADERS(string.h)
AC_CHECK_HEADERS(strings.h)
AC_CHECK_HEADERS(malloc.h)
AC_CHECK_HEADERS(netinet/in/system.h)
AC_CHECK_HEADERS(netinet/in/systm.h)
AC_CHECK_HEADERS(netinet/ip.h)
AC_CHECK_HEADERS(netinet/tcp.h)
AC_CHECK_HEADERS(paths.h)
AC_CHECK_HEADERS(sys/resource.h)
AC_CHECK_HEADERS(sys/select.h)
AC_CHECK_HEADERS(sys/time.h)
AC_CHECK_HEADERS(unistd.h)
AC_HEADER_DIRENT

AC_HEADER_SYS_WAIT
AC_CHECK_FUNCS(waitpid)
AC_CHECK_FUNCS(wait3)
AC_HEADER_STDC

AC_REPLACE_FUNCS(setenv)
AC_REPLACE_FUNCS(unsetenv)

AC_ARG_WITH(initscript-config-path,
            AC_HELP_STRING([--with-initscript-config-path=PATH],
            [Set the path to init script configuration. By default, this package
             will install its configuration file in
             ${sysconfdir}/globus-gatekeeper.conf. 
             ]),
            [if test "$withval" = "yes";  then
                AC_MSG_ERROR([Invalid path: $withval])
            else
                GATEKEEPER_CONFIGDIR=`dirname $withval`
                GATEKEEPER_CONFIGFILE=`basename $withval`
            fi],
            [
                GATEKEEPER_CONFIGDIR='${sysconfdir}'
                GATEKEEPER_CONFIGFILE="globus-gatekeeper.conf"
            ])

AC_ARG_WITH(lockfile-path,
            AC_HELP_STRING([--with-lockfile-path=PATH],
            [Set the path to init script lockfile]),
            [if test "$withval" = "yes";  then
                AC_MSG_ERROR([Invalid path: $withval])
            else
                LOCKFILE="$withval"
            fi],
            [
                LOCKFILE="\${localstatedir}/lock/globus-gatekeeper"
            ])
AC_SUBST(LOCKFILE)

LSB="-nolsb"
AC_ARG_WITH(lsb,
        AC_HELP_STRING(
            [--with-lsb],
            [Enable use of LSB support functions in the globus-gatekeeper
            init script. If set, it will use linux-specific functions to start
            and stop the gatekeeper service and log its status changes. This
            script will probe for the LSB base scripts if this is not set.
            If not found, it will use a generic service startup script.]),
            [ if test "$withval" = yes; then
                LSB="-lsb"
              fi],
            [
                if test -f /lib/lsb/init-functions; then
                    LSB="-lsb"
                fi
            ])

AC_SUBST(LSB)
AC_SUBST(GATEKEEPER_CONFIGDIR)
AC_SUBST(GATEKEEPER_CONFIGFILE)

xml_catalog_path="notset"
AC_ARG_WITH(
    xml-catalog,
    AC_HELP_STRING([--with-xml-catalog=PATH],
    [Path to XML catalog. If this option is used, manual pages will be
    generated from the XML documentation files.]),
    [xml_catalog_path="$withval"],
    [xml_catalog_path="notset"])


AC_PATH_PROG(XSLTPROC, "xsltproc")
AC_PATH_PROG(XMLLINT, "xmllint")

if test "x$xml_catalog_path" != x"notset"; then
    if test ! -f "$xml_catalog_path"; then
        AC_MSG_ERROR([Invalid XML catalog path])
    else
        XML_CATALOG_PATH=$xml_catalog_path
        AC_SUBST(XML_CATALOG_PATH)
    fi
fi

AC_ARG_WITH(default-runlevels,
        AC_HELP_STRING(
            [--with-default-runlevels=RUNLEVELS],
            [Choose which System-V style runlevels the gatekeeper service will
             be started on.]),
            [ case "$withval" in
                yes)
                  AC_MSG_WARN([Invalid parameter to --with-default-runlevels])
                  ;;
                no)
                  CHKCONFIG_DEFAULT="-"
                  DEFAULT_START=""
                  DEFAULT_STOP=""
                  ;;
                *)
                  CHKCONFIG_DEFAULT="$withval"
                  for r in 0 1 2 3 4 5 6; do
                      if expr $withval : ".*$r.*" > /dev/null; then
                          DEFAULT_START="${DEFAULT_START:+$DEFAULT_START }$r"
                      else
                          DEFAULT_STOP="${DEFAULT_STOP:+$DEFAULT_STOP }$r"
                      fi
                  done
              esac],
            [
                CHKCONFIG_DEFAULT="-"
                DEFAULT_START=""
                DEFAULT_STOP=""
            ])

if test "$DEFAULT_START" = ""; then
    DEFAULT_START_LINE="#"
else
    DEFAULT_START_LINE="# Default-Start:     $DEFAULT_START"
fi
if test "$DEFAULT_STOP" = ""; then
    DEFAULT_STOP_LINE="#"
else
    DEFAULT_STOP_LINE="# Default-Stop:      $DEFAULT_STOP"
fi

AC_SUBST(CHKCONFIG_DEFAULT)
AC_SUBST(DEFAULT_START_LINE)
AC_SUBST(DEFAULT_STOP_LINE)
AC_SUBST(LSB)

LOGDIR="`eval_path $localstatedir/log`"
AC_SUBST(LOGDIR)

AM_CONDITIONAL([GENERATE_MANPAGES], [test "$xml_catalog_path" != "notset"])

GLOBUS_FINALIZE

AC_OUTPUT(
	pkgdata/Makefile
	pkgdata/pkg_data_src.gpt
        init/Makefile
        init/globus-gatekeeper-lsb
        init/globus-gatekeeper-nolsb
        config/Makefile
        config/globus-gatekeeper
        config/$GATEKEEPER_CONFIGFILE:config/globus-gatekeeper.in
        gatekeeper.logrotate
        Makefile
)
