AC_REVISION($Revision$)
AC_INIT(Makefile.am)
AC_CONFIG_AUX_DIR(config)

AC_PREFIX_DEFAULT(${GLOBUS_LOCATION:-/usr})

GPT_LOCATION="${GPT_LOCATION:-${GLOBUS_LOCATION:-/usr}}"

# save CC and CFLAGS, so AC_PROG_CC can detect and use a compiler
# up to the point where we want to pick a compiler based on the
# flavor  (see config/accompiler.m4)
SAVED_CC="$CC"
SAVED_CFLAGS="$CFLAGS"

# AIX: we don't want OBJECT_MODE to affect the compiler detection
# if gcc is found and OBJECT_MODE=64, things will likely fail
unset OBJECT_MODE

# Call init macros

GPT_INIT
AM_INIT_AUTOMAKE($GPT_NAME, $GPT_VERSION,0)
AM_MAINTAINER_MODE
AC_CONFIG_HEADERS(globus_config.h:globus_config.h.in)

AC_ARG_WITH(
    flavor,
    AC_HELP_STRING([--with-flavor], [Globus flavor string]),
    [globus_cv_flavor="$withval"],
    [globus_cv_flavor="notset"])


if test "$globus_cv_flavor" = "notset" \
     -o "$globus_cv_flavor" = "no" \
     -o "$globus_cv_flavor" = "yes" ; then
    AC_MSG_ERROR([A flavor description string must be specified!])
fi

  
AC_ARG_WITH([setupdir],
AC_HELP_STRING([--with-setupdir=DIR],
[setup installation directory [[PREFIX/setup/globus]]]),
[
    if test x$withval = "xno" -o x$withval = "xyes" ; then
        AC_MSG_ERROR([--with-setupdir requires an argument])
    fi
    setupdir=$withval
],
[
    setupdir='${prefix}/setup/globus'
])
AC_SUBST(setupdir)

AC_ARG_WITH([testdir],
AC_HELP_STRING([--with-testdir=DIR],
[test installation directory [[PREFIX/test/PACKAGE]]]),
[
    if test x$withval = "xno" -o x$withval = "xyes" ; then
        AC_MSG_ERROR([--with-testdir requires an argument])
    fi
    testdir=$withval
],
[
    testdir='${prefix}/test/${PACKAGE}'
])
AC_SUBST(testdir)

AC_ARG_WITH([flavorincludedir],
AC_HELP_STRING([--with-flavorincludedir=DIR],
[flavored header installation directory [[INCLUDEDIR/FLAVOR]]]),
[
    if test x$withval = "xno" -o x$withval = "xyes" ; then
        AC_MSG_ERROR([--with-flavorincludedir requires an argument])
    fi
    flavorincludedir=$withval
],
[
    flavorincludedir='${includedir}/${GLOBUS_FLAVOR_NAME}'
])
AC_SUBST(flavorincludedir)

AC_ARG_WITH([perlmoduledir],
AC_HELP_STRING([--with-perlmoduledir=DIR],
[perl module directory [[PREFIX/lib/perl]]]),
[
    if test x$withval = "xno" -o x$withval = "xyes" ; then
        AC_MSG_ERROR([--with-perlmoduledir requires an argument])
    fi
    perlmoduledir=$withval
],
[
    perlmoduledir='${prefix}/lib/perl'
])
AC_SUBST(perlmoduledir)

AC_ARG_WITH([doxygendir],
AC_HELP_STRING([--with-doxygendir=DIR],
[doxygen installation directory [[DATADIR/doxygen]]]),
[
    if test x$withval = "xno" -o x$withval = "xyes" ; then
        AC_MSG_ERROR([--with-doxygendir requires an argument])
    fi
    doxygendir=$withval
],
[
    doxygendir='${datadir}/doxygen'
])
AC_SUBST(doxygendir)

aclocaldir='${datadir}/globus/aclocal'
pkgdir='${datadir}/globus/packages/${PACKAGE}'
flavordir='${datadir}/globus/flavors'

AC_SUBST(aclocaldir)
AC_SUBST(pkgdir)
AC_SUBST(flavordir)

AC_ARG_WITH([initializer-libdir-based-on-machine-type],
AC_HELP_STRING([--with-initializer-libdir-based-on-machine-type],
[Guess that the libdir might be lib or lib64 depending on the machine type]),
[
    case $withval in
        yes)
            guess_libdir=1
            ;;
        *)
            guess_libdir=0
            ;;
    esac
],
[
    guess_libdir=0
])
AC_SUBST(guess_libdir)

GLOBUS_FLAVOR_NAME="$globus_cv_flavor"
AC_SUBST(GLOBUS_FLAVOR_NAME)

LAC_COMPILERS

aixargs=""
if test "x$AIX" = "xyes"; then
    case $flavor in
        *64*)
            aixargs="export OBJECT_MODE=64"
            ;;
        *32*)
            aixargs="export OBJECT_MODE=32"
            ;;
    esac
fi
AC_SUBST(aixargs,$aixargs)
LAC_TARGET_ARCH
AC_SYS_LARGEFILE
CHECK_HEADERS
CHECK_FUNCS
CHECK_FOR_TIMESPEC
CHECK_FOR_IOVEC
CHECK_FOR_ATEXIT
CHECK_FOR_MEMMOVE
CHECK_SIZES

if test "X$GPT_LINKTYPE" = "Xstatic"; then
    AC_DEFINE(BUILD_STATIC_ONLY)
fi

GLOBUS_INITIALIZERS

dnl define FILELIST_FILE variable
FILELIST_FILE=`pwd`;
FILELIST_FILE="$FILELIST_FILE/pkgdata/master.filelist"
AC_SUBST(FILELIST_FILE)

dnl go look for perl
if test "x$PERL" = "x"; then
        AC_PATH_PROG(PERL,perl)
fi


AC_ARG_ENABLE(am-depends,
    AC_HELP_STRING([--enable-am-depends], [Enable automake dependency caching.  (Requires gcc, developers only)]),
[
	if test "X$GCC" = "Xyes"; then
		AUTOMAKE_OPTS=""
	else
		AUTOMAKE_OPTS="no-dependencies"
	fi
],
[
	AUTOMAKE_OPTS="no-dependencies"
])

AC_SUBST(AUTOMAKE_OPTS)


AM_PROG_LIBTOOL

dnl
dnl Output selected thread package
dnl
GLOBUS_THREADS=$lac_cv_threads_type
if test "$GLOBUS_THREADS" = "no" ; then
    GLOBUS_THREADS=none
fi

AC_SUBST(GLOBUS_THREADS)

dnl Automake is using these for executables somehow
AC_OBJEXT
AC_EXEEXT

dnl Save the flavor metadata
CONFIG_PARAMETERS="$ac_configure_args"
AC_SUBST(CONFIG_PARAMETERS)

ISGCC=$ac_cv_prog_gcc
AC_SUBST(ISGCC)

dnl  Another hack - set LD to whatever libtool picked up, so that
dnl  it can be registered in build-parameters
if test "x-$lt_cv_path_LD" != "x-"; then
    LD="$lt_cv_path_LD"
    AC_SUBST(LD)
fi

AC_OUTPUT(Makefile
	scripts/Makefile
        scripts/globus-build-env-$GLOBUS_FLAVOR_NAME.sh:scripts/globus-build-env.sh.in
        scripts/globus-build-env-noflavor.sh:scripts/globus-build-env-noflavor.sh.in
        scripts/globus-bootstrap.sh
	aclocal/Makefile
	aclocal/automake_config
	config/Makefile
	doxygen/Makefile
	doxygen/globus-update-doxygen-index
	doxygen/globus-build-doxygen-dependencies
	pkgdata/Makefile
	pkgdata/pkg_data_src.gpt
	pkgdata/build-parameters
	)

