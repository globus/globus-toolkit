AC_REVISION($Revision$)
AC_INIT(Makefile.am)
AC_CONFIG_AUX_DIR(config)

# checking for the GLOBUS_LOCATION

if test "x$GLOBUS_LOCATION" = "x"; then
    echo "ERROR Please specify GLOBUS_LOCATION" >&2
    exit 1
fi

if test "x$GPT_LOCATION" = "x"; then
    GPT_LOCATION=$GLOBUS_LOCATION
    export GPT_LOCATION
fi

prefix='$(GLOBUS_LOCATION)'
exec_prefix='$(GLOBUS_LOCATION)'

# save CC and CFLAGS, so AC_PROG_CC can detect and use a compiler
# up to the point where we want to pick a compiler based on the
# flavor  (see config/accompiler.m4)
SAVED_CC="$CC"
SAVED_CFLAGS="$CFLAGS"

# AIX: we don't want OBJECT_MODE to affect the compiler detection
# if gcc is found and OBJECT_MODE=64, things will likely fail
unset OBJECT_MODE

# Call init macros

GPT_INIT
AM_INIT_AUTOMAKE($GPT_NAME, $GPT_VERSION,0)
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(globus_config.h:globus_config.h.in)

AC_ARG_WITH(
    flavor,
    [  --with-flavor                 Globus flavor string],
    [globus_cv_flavor="$withval"],
    [globus_cv_flavor="notset"])


if test "$globus_cv_flavor" = "notset" \
     -o "$globus_cv_flavor" = "no" \
     -o "$globus_cv_flavor" = "yes" ; then
    AC_MSG_ERROR([A flavor description string must be specified!])
fi

LAC_COMPILERS
LAC_TARGET_ARCH
AC_SYS_LARGEFILE
CHECK_HEADERS
CHECK_FUNCS
CHECK_FOR_TIMESPEC
CHECK_FOR_IOVEC
CHECK_FOR_ATEXIT
CHECK_FOR_MEMMOVE
CHECK_SIZES

if test "X$GPT_LINKTYPE" = "Xstatic"; then
    AC_DEFINE(BUILD_STATIC_ONLY)
fi

GLOBUS_FLAVOR_NAME=$globus_cv_flavor
AC_SUBST(GLOBUS_FLAVOR_NAME)

dnl define FILELIST_FILE variable
FILELIST_FILE=`pwd`;
FILELIST_FILE="$FILELIST_FILE/pkgdata/master.filelist"
AC_SUBST(FILELIST_FILE)

dnl go look for perl
if test "x$PERL" = "x"; then
        AC_PATH_PROG(PERL,perl)
fi


AC_ARG_ENABLE(internal-doc,
[  --enable-am-depends		Enable automake dependency caching.
				(Requires gcc, developers only)],
[
	if test "X$GCC" = "Xyes"; then
		AUTOMAKE_OPTS=""
	else
		AUTOMAKE_OPTS="no-dependencies"
	fi
],
[
	AUTOMAKE_OPTS="no-dependencies"
])

AC_SUBST(AUTOMAKE_OPTS)


AM_PROG_LIBTOOL
mv ./libtool libtool-$GLOBUS_FLAVOR_NAME

dnl
dnl Output selected thread package
dnl
GLOBUS_THREADS=$lac_cv_threads_type
if test "$GLOBUS_THREADS" = "no" ; then
    GLOBUS_THREADS=none
fi

AC_SUBST(GLOBUS_THREADS)

dnl Automake is using these for executables somehow
AC_OBJEXT
AC_EXEEXT

dnl Save the flavor metadata
CONFIG_PARAMETERS="$ac_configure_args"
AC_SUBST(CONFIG_PARAMETERS)

ISGCC=$ac_cv_prog_gcc
AC_SUBST(ISGCC)

dnl  Another hack - set LD to whatever libtool picked up, so that
dnl  it can be registered in build-parameters
if test "x-$lt_cv_path_LD" != "x-"; then
    LD="$lt_cv_path_LD"
    AC_SUBST(LD)
fi

AC_OUTPUT(Makefile
       	scripts/globus-build-env-$GLOBUS_FLAVOR_NAME.sh:scripts/globus-build-env.sh.in
	scripts/Makefile
	aclocal/Makefile
	aclocal/automake_config
	config/Makefile
	doxygen/Makefile
	pkgdata/Makefile
	pkgdata/pkg_data_src.gpt
	pkgdata/build-parameters
	)

