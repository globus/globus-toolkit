dnl Intended to be used with a Makefile.in generated from installer.sh
AC_INIT(Globus Toolkit, 999)
AC_CONFIG_FILES(Makefile)
AC_PREFIX_DEFAULT(/usr/local/globus-4.2.0)
AC_CANONICAL_BUILD
if test "x$flavor" = "x"; then
   case "$build" in
   i*86-*-*)
      flavor=gcc32dbg
   ;;
   power*-ibm-aix5.*)
      flavor=vendorcc32dbg
      AIX=yes
   ;;
   ppc-*-*-*)
      flavor=gcc32dbg
   ;;
   power*-apple-darwin*)
      flavor=gcc32dbg
   ;;
   sparc-sun-solaris*)
      flavor=gcc32dbg
   ;;
   sparc64-*-*)
      flavor=gcc64dbg
   ;;
   ia64-*-*)
      flavor=gcc64dbg
   ;; 
   x86_64-*-*-*)
      flavor=gcc64dbg
   ;;
   amd64-*-*)
      flavor=gcc64dbg
   ;;
   s390-ibm-linux-*)
      flavor=gcc32dbg
   ;;
   alphaev*-dec-osf*)
      flavor=vendorcc64dbg
   ;;
   esac
fi

AC_ARG_WITH(flavor, AC_HELP_STRING([--with-flavor=flavor],\
   [Use flavor as the base for GPT builds.]),
   [flavor=$withval], [])

if test "x$flavor" = "x"; then
   echo "Unable to guess flavor. Please use --with-flavor= to specify one"
   exit 1
fi
thr=pthr
AC_SUBST(flavor, $flavor)
AC_SUBST(thr, $thr)

aixargs=""
if test "x$AIX" = "xyes"; then
    case $flavor in
        *64*)
            aixargs="export OBJECT_MODE=64"
            ;;
        *32*)
            aixargs="export OBJECT_MODE=32"
            ;;
    esac
fi
AC_SUBST(aixargs,$aixargs)

gridshib=""
AC_ARG_ENABLE(gridshib, AC_HELP_STRING([--enable-gridshib],\
   [Enable the build of gridshib.  Default is disabled.]),\
   [if test $enableval = "yes"; then gridshib="gridshib_bundle"; fi], [])
AC_SUBST(gridshib, $gridshib)

openssl="globus_system_openssl"
AC_ARG_ENABLE(system-openssl, AC_HELP_STRING([--disable-system-openssl],\
   [Use the globus_openssl package instead of system openssl.  Default is to use the system openssl.]),\
   [if test $enableval = "no"; then openssl="globus_openssl_backup"; fi], [])
AC_SUBST(globus_openssl, $openssl)

AC_ARG_WITH(versionname, AC_HELP_STRING([--with-versionname="packager"],\
   [The name to be displayed by globus-version.  Default is "Globus Toolkit".]),
\
   [versionname=$withval], [])
AC_SUBST(versionname, $versionname)

AC_ARG_WITH(buildopts, AC_HELP_STRING([--with-buildopts="args"],\
   [Arguments to pass to gpt-build, like "--verbose" or "--force".]),\
   [buildopts=$withval], [])
AC_SUBST(buildopts, $buildopts)

AC_ARG_WITH(gptlocation, AC_HELP_STRING([--with-gptlocation=dir],\
   [If you want to install GPT into an alternate directory.]),\
   [gptlocation=$withval], [if test x$prefix = "xNONE"; then\
   gptlocation=$ac_default_prefix; else gptlocation=$prefix; fi])
AC_SUBST(gptlocation, $gptlocation)
dnl AC_SUBST(globusloc, prefix)
dnl Pre-webservices code
AC_ARG_WITH(iodbc-includes, AC_HELP_STRING([--with-iodbc-includes=dir],\
   [Specify where to find [IODBC] include files.  [IODBC] and unixODBC are optional for [RLS] builds.]),
   [[GLOBUS_IODBC_INCLUDES]=$withval], [])
AC_SUBST(globus_iodbc_includes, $[GLOBUS_IODBC_INCLUDES])
AC_ARG_WITH(iodbc-libs, AC_HELP_STRING([--with-iodbc-libs=dir],\
   [Specify where to find [IODBC] libraries.  [IODBC] and unixODBC are optional for [RLS] builds.]),
   [[GLOBUS_IODBC_LIBS]=$withval], [])
AC_SUBST(globus_iodbc_libs, $[GLOBUS_IODBC_LIBS])
AC_ARG_WITH(unixodbc-includes, AC_HELP_STRING([--with-unixodbc-includes=dir],\
   [Specify where to find unixODBC include files.  [IODBC] and unixODBC are optional for [RLS] builds.]),
   [[GLOBUS_UNIXODBC_INCLUDES]=$withval], [])
AC_SUBST(globus_unixodbc_includes, $[GLOBUS_UNIXODBC_INCLUDES])
AC_ARG_WITH(unixodbc-libs, AC_HELP_STRING([--with-unixodbc-libs=dir],\
   [Specify where to find unixODBC libraries.  [IODBC] and unixODBC are optional for [RLS] builds.]),
   [[GLOBUS_UNIXODBC_LIBS]=$withval], [])
AC_SUBST(globus_unixodbc_libs, $[GLOBUS_UNIXODBC_LIBS])

wsgramcondor=" "
AC_ARG_ENABLE(wsgram-condor, AC_HELP_STRING([--enable-wsgram-condor],\
   [Build GRAM Condor scheduler interface.  Default is disabled.]),
   [if test $enableval = "yes"; then wsgramcondor="gt4-gram-condor"; fi], [])
AC_SUBST(wsgramcondor, $wsgramcondor)
wsgramlsf=" "
AC_ARG_ENABLE(wsgram-lsf, AC_HELP_STRING([--enable-wsgram-lsf],\
   [Build GRAM LSF scheduler interface.  Default is disabled.]),
   [if test $enableval = "yes"; then wsgramlsf="gt4-gram-lsf"; fi], [])
AC_SUBST(wsgramlsf, $wsgramlsf)
wsgrampbs=" "
AC_ARG_ENABLE(wsgram-pbs, AC_HELP_STRING([--enable-wsgram-pbs],\
   [Build GRAM PBS scheduler interface.  Default is disabled.]),
   [if test $enableval = "yes"; then wsgrampbs="gt4-gram-pbs"; fi], [])
AC_SUBST(wsgrampbs, $wsgrampbs)

i18n=" "
AC_ARG_ENABLE(i18n, AC_HELP_STRING([--enable-i18n],\
   [Enable internationalization.  Default is disabled.]),
   [if test $enableval = "yes"; then i18n="i18n"; fi],
   [])
AC_SUBST(i18n, $i18n)

AC_ARG_WITH(gsiopensshargs, AC_HELP_STRING([--with-gsiopensshargs="args"],\
   [Arguments to pass to the build of GSI-OpenSSH, like --with-tcp-wrappers]),
   [gsiopensshargs="$withval"], [gsiopensshargs="--without-zlib-version-check"])
AC_SUBST(gsiopensshargs, $gsiopensshargs)

netlogger=""
AC_ARG_ENABLE(netlogger, AC_HELP_STRING([--enable-netlogger],\
   [Build netlogger libraries.  Default is disabled.]),
   [if test $enableval = "yes"; then netlogger="netlogger_c"; fi], [])
AC_SUBST(netlogger, $netlogger)

# java
AC_PATH_PROG(JAVAC_PATH, javac)
if test "x$JAVAC_PATH" = "x"; then
    AC_MSG_WARN(A Java compiler is needed for some parts of the toolkit) 
    AC_MSG_WARN(This message can be ignored if you are only building the C parts of the toolkit)
else
    if test "x$JAVA_HOME" = "x"; then
        AC_MSG_WARN(JAVA_HOME is not set) 
        AC_MSG_WARN(Most Java versions will not work correctly without JAVA_HOME set)
    else
        if test "$JAVAC_PATH" != "$JAVA_HOME/bin/javac"; then
            AC_MSG_WARN(the javac in your path is not from your \$JAVA_HOME environment)
        fi
    fi
fi

# ant
AC_PATH_PROG(ANT_PATH, ant)
if test "x$ANT_PATH" = "x"; then
   AC_MSG_WARN(ant is needed for some parts of the toolkit) 
   AC_MSG_WARN(If you know you will not need one, this message can be ignored)
fi

AC_OUTPUT


