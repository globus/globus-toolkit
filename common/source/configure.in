dnl

AC_REVISION($Revision$)
AC_INIT(Makefile.am)

GLOBUS_INIT

AM_PROG_LIBTOOL

dnl Initialize the automake rules the last argument
AM_INIT_AUTOMAKE($GPT_NAME, $GPT_VERSION)

AC_CONFIG_HEADERS(config.h)

LAC_THREADS

CHECK_REENTRANT_FUNCS

AC_SEARCH_LIBS(socket, socket)
AC_SEARCH_LIBS(gethostbyname, nsl)

LT_LIB_DLLOAD

AC_CHECK_LIB(ltdl, lt_dlerror)
AC_CHECK_FUNCS(lt_dlmutex_register)

GPT_SET_LIBS(["$EXTERNAL_LIBS"])

if test "X$GLOBUS_VERSION" = "X"; then
    if test -x ${GLOBUS_LOCATION}/bin/globus-version; then
        GLOBUS_VERSION="`${GLOBUS_LOCATION}/bin/globus-version`"
        if test X"$GLOBUS_VERSIONNAME" = "X" ; then
            GLOBUS_VERSION_FULL="`${GLOBUS_LOCATION}/bin/globus-version -full`"
            GLOBUS_VERSIONNAME="`echo $GLOBUS_VERSION_FULL | sed "s/ $GLOBUS_VERSION//"`"
        fi
    else
        GLOBUS_VERSION="$DIRT_TIMESTAMP-$DIRT_BRANCH_ID"
    fi
fi

if test X"$GLOBUS_VERSIONNAME" = "X" ; then
    GLOBUS_VERSIONNAME="Globus Toolkit"
fi

AC_SUBST(GLOBUS_VERSION)
AC_SUBST(GLOBUS_VERSIONNAME)

LAC_DOXYGEN("../library")

AC_PATH_PROGS(GLOBUS_SH_PERL, perl perl5)
AC_PATH_PROGS(GLOBUS_SH, sh bash ksh)

AC_ARG_WITH([backward-compatibility-hack],
    AC_HELP_STRING(
        [--with-backward-compatibility-hack],
        [Enable binary-backward compatibility with GT 5.0.x by adding symbol
         aliases for different library versions]),
    [case "$GLOBUS_HOST" in
    *-*-linux* | *-*-gnu* | *-*-k*bsd*-gnu)
        if test "$withval" = yes; then
            AC_MSG_NOTICE([Using backward-compatibility symbol labels])
            use_symbol_labels="yes"
        fi
        ;;
    *)
        if test "$withval" = yes; then
            AC_MSG_NOTICE([Ignoring --with-backward-compatibility-hack on unsuppored operating system/linker])
        fi
    esac],
        [use_symbol_labels="no"])

AM_CONDITIONAL(USE_SYMBOL_LABELS, test "$use_symbol_labels" = "yes")

if test "$use_symbol_labels" = yes; then
    AC_DEFINE(USE_SYMBOL_LABELS, 1, [Use backward-compatibility symbol labels])
    GPT_EXTERNAL_LIBS=-lpthread
    AC_SUBST(GPT_EXTERNAL_LIBS)
fi

xml_catalog_path="notset"
AC_ARG_WITH(
    xml-catalog,
    AC_HELP_STRING(
        [--with-xml-catalog=PATH],
        [Look for XML catalog in PATH]),
    [xml_catalog_path="$withval"],
    [xml_catalog_path="notset"])

AC_PATH_PROG(XSLTPROC, "xsltproc")
AC_PATH_PROG(XMLLINT, "xmllint")

if test "x$xml_catalog_path" != x"notset"; then
    if test ! -f "$xml_catalog_path"; then
        AC_MSG_ERROR([Invalid XML catalog path])
    else
        XML_CATALOG_PATH=$xml_catalog_path
        AC_SUBST(XML_CATALOG_PATH)
    fi
fi

AM_CONDITIONAL(BUILD_MANPAGES, [
                test "$xml_catalog_path" != "notset" && \
                test "$XSLTPROC" != "no" && \
                test "$XMLLINT" != "no"])

GLOBUS_FINALIZE

AC_OUTPUT(
	Makefile
	pkgdata/Makefile
	pkgdata/pkg_data_src.gpt
	library/Makefile
        library/globus_l_common_paths.h
	programs/Makefile
        programs/globus-sh-exec
        programs/globus-version
	scripts/Makefile
        scripts/Paths.pm
        scripts/globus-domainname:scripts/globus-hostname.in
        scripts/globus-hostname
        scripts/globus-makefile-header
        scripts/globus-sh-tools.sh
        scripts/globus-sh-tools-vars.sh
        scripts/globus-script-initializer
	config/Makefile
        manpages/Makefile
	doxygen/Makefile
	doxygen/Doxyfile 
	doxygen/Doxyfile-internal
	version.h
)
