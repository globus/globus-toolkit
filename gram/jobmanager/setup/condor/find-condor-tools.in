AC_REVISION($Revision$)
AC_INIT(condor.in)

# checking for the GLOBUS_LOCATION

if test "x$GLOBUS_LOCATION" = "x"; then
    echo "ERROR Please specify GLOBUS_LOCATION" >&2
    exit 1
fi

AC_PATH_PROG(CONDOR_SUBMIT, condor_submit, no)
if test "$CONDOR_SUBMIT" = "no" ; then
    AC_MSG_ERROR("Cannot locate condor_submit")
fi

AC_PATH_PROG(CONDOR_RM, condor_rm, no)
if test "$CONDOR_RM" = "no"; then
    AC_MSG_ERROR("Cannot locate condor_rm")
fi

AC_ARG_WITH(condor-os,
            [ --with-condor-os=OS],
	    [if test "$withval" != yes; then
	         CONDOR_OS=$withval
	     else
	         CONDOR_OS=no
	     fi],
	    [CONDOR_OS=no])

AC_ARG_WITH(condor-arch,
            [ --with-condor-arch=ARCH],
	    [if test "$withval" != yes; then
	         CONDOR_ARCH=$withval
	     else
	         CONDOR_ARCH=no
	     fi],
	    [CONDOR_ARCH=no])

AC_ARG_WITH(check-vanilla-files,
            [ --with-check-vanilla-files],
	    [CONDOR_CHECK_VANILLA_FILES=$withval],
	    [CONDOR_CHECK_VANILLA_FILES=no])

AC_ARG_WITH(mpi-script,
            [ --with-mpi-script=PATH-TO-MPI-SCRIPT],
	    [if test "$withval" != yes; then
	         CONDOR_MPI_SCRIPT=$withval
	     else
	         CONDOR_MPI_SCRIPT=no
	     fi],
	    [CONDOR_MPI_SCRIPT=no])


prefix='$(GLOBUS_LOCATION)'
exec_prefix='$(GLOBUS_LOCATION)'
libexecdir=${prefix}/libexec

UNAME_MACHINE=`(uname -m) 2> /dev/null ` || UNAME_MACHINE=unknown
UNAME_RELEASE=`(uname -r) 2> /dev/null ` || UNAME_RELEASE=unknown
UNAME_SYSTEM=`(uname -s) 2> /dev/null ` || UNAME_SYSTEM=unknown
UNAME_VERSION=`(uname -v) 2> /dev/null ` || UNAME_VERSION=unknown

if test $CONDOR_OS = 'no'; then
    case "${UNAME_MACHINE}:${UNAME_SYSTEM}:${UNAME_RELEASE}" in
       *:FreeBSD:6*)
         CONDOR_OS=FREEBSD6
         ;;
       *:Linux:*)
	 CONDOR_OS=LINUX
	 ;;
       *:OSF1:*)
	 CONDOR_OS=OSF1
	 ;;
       *:SunOS:5.[[6-9]])
	 CONDOR_OS=SOLARIS`echo ${UNAME_RELEASE} | sed -e 's:5\.\(.\):2\1:'`
	 ;;
       *:SunOS:5.10)
         CONDOR_OS=SOLARIS5.10
         ;;
       *:HP-UX:*)
         CONDOR_OS=HPUX11
         ;;
       *:Darwin:*)
         CONDOR_OS=OSX
         ;;
       *:AIX:*)
         CONDOR_OS=AIX${UNAME_VERSION}${UNAME_RELEASE}
         ;;
       *)
         AC_MSG_ERROR([unknown CONDOR_OS])
	 ;;
    esac
fi

if test $CONDOR_ARCH = 'no'; then
    case "${UNAME_MACHINE}:${UNAME_SYSTEM}:${UNAME_RELEASE}" in
        alpha:*:*)
	  CONDOR_ARCH=ALPHA
	  ;;
        x86_64:*:*)
          CONDOR_ARCH=x86_64
          ;;
        i?86*:*:*)
	  CONDOR_ARCH=INTEL
	  ;;
        sun4*:*:*)
	  CONDOR_ARCH=`uname -m | sed -e 's:sun4\(.\):SUN4\1:'`
	  ;;
        9000/?00:*:*)
          CONDOR_ARCH=HPPA2
          ;;
        ia64:*:*)
          CONDOR_ARCH=IA64
          ;;
        "Power Macintosh":*:*)
          CONDOR_ARCH=PPC
          ;;
         *:AIX:[[23]]) 
          CONDOR_ARCH=PWR3II 
          ;; 
       *)
         AC_MSG_ERROR([unknown CONDOR_ARCH])
      esac
fi

if test "$CONDOR_CONFIG" = ''; then
    AC_MSG_WARN([CONDOR_CONFIG not set in environment, condor will search default paths])
fi
AC_SUBST(CONDOR_CONFIG)
AC_SUBST(CONDOR_OS)
AC_SUBST(CONDOR_ARCH)
AC_SUBST(CONDOR_CHECK_VANILLA_FILES)
AC_SUBST(CONDOR_MPI_SCRIPT)

AC_OUTPUT(
    condor.pm:condor.in
    globus-condor-print-config
)
